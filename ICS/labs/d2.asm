.ORIG X3000
LD R6,STACK
LD R0,MAP
LDR R1,R0,#-2
ST  R1,N
NOT R1,R1
ADD R1,R1,#1
ST R1,NEGN
LDR R1,R0,#-1
ST  R1,M
NOT R1,R1
ADD R1,R1,#1
ST R1,NEGM


AND R5,R5,#0 ; ANS =1 
AND R1,R1,#0 ; R1 =i  
AND R2,R2,#0 ; R2 =j
LD  R4,INF  
WHILE1
    LD R3,NEGN
    ADD R3,R1,R3 ; I -N
    BRZ WHILE1OUT
        WHILE2
        LD R3,NEGM
        ADD R3,R2,R3 ; J - M
        BRZ WHILE2OUT
        ;; ANS = MAX (ANS , DFS(I,J,-1))
        ST  R1,K1
        ST  R5,K2
        JSR DFS ; CALL (DFS(I,J,-1))
        LD R5,K2
        ADD R1,R5,#0 ; R1 = ANS 
        JSR GETMAX ; R0 = MAX (R1,R0)
        ADD R5,R0,#0 ; R5 = MAX
        LD R1,K1
        ADD R2,R2,#1
        BR WHILE2
    WHILE2OUT
    AND R2,R2,#0 ; R2 =j
    ADD R1,R1,#1
    BR WHILE1
WHILE1OUT
ADD R2,R5,#0
HALT
K2 .BLKW 1
K1 .BLKW 1
M .BLKW 1
N .BLKW 1
NEGM .BLKW 1
NEGN .BLKW 1
MAP .FILL X4002
STACK .FILL X8000
OFFSET .FILL X1000
INF .FILL XC000

DFS ; R0 AS OUTPUT , R1,R2 AS IJ R4 AS PRE
ADD R6,R6,#-1
STR R7,R6,#0
ADD R6,R6,#-1
STR R1,R6,#0
ADD R6,R6,#-1
STR R2,R6,#0
ADD R6,R6,#-1
STR R3,R6,#0
ADD R6,R6,#-1
STR R5,R6,#0
ADD R6,R6,#-1
STR R4,R6,#0

ADD R1,R1,#0
BRN RETURN0
ADD R2,R2,#0
BRN RETURN0
LD R3,NEGN
ADD R3,R3,R1 ;
BRZP RETURN0
LD R3,NEGM
ADD R3,R3,R2
BRZP RETURN0
JSR GETMAPIJ

ADD R3,R4,#0 ;
NOT R3,R3
ADD R3,R3,#1 ; -PRE
LDR R5,R0,#0 ; R5 = MATRIX[I][J]
ADD R3,R3,R5 ; 
BRNZ RETURN0

ADD R3,R0,#0 ;
LD R0,OFFSET ;
ADD R3,R0,R3 ; R3 = &MARK[i][j]
LDR R0,R3,#0 ; R0 = MARK[I][J]
BRNP RETURN

ADD R4,R5,#0
ADD R1,R1,#-1
JSR DFS
ADD R5,R0,#0

ADD R1,R1,#2
JSR DFS
ST  R1,K3
ADD R1,R5,#0
JSR GETMAX
LD  R1,K3
ADD R5,R0,#0

ADD R1,R1,#-1
ADD R2,R2,#-1
JSR DFS
ST  R1,K3
ADD R1,R5,#0
JSR GETMAX
LD  R1,K3
ADD R5,R0,#0

ADD R2,R2,#2
JSR DFS
ST  R1,K3
ADD R1,R5,#0
JSR GETMAX
LD  R1,K3
ADD R5,R0,#0

ADD R2,R2,#-1
ADD R5,R5,#1 ; TEM++
STR R5,R3,#0 ; MARKIJ = TEM
BR RETURN

RETURN0

AND R0,R0,#0

LDR R4,R6,#0
ADD R6,R6,#1
LDR R5,R6,#0
ADD R6,R6,#1
LDR R3,R6,#0
ADD R6,R6,#1
LDR R2,R6,#0
ADD R6,R6,#1
LDR R1,R6,#0
ADD R6,R6,#1
LDR R7,R6,#0
ADD R6,R6,#1
RET

RETURN
JSR GETMAPIJ
LD R3,OFFSET 
ADD R0,R3,R0 ;R0 = &MARK[][]
LDR R0,R0,#0 ;RETURN MARKIJ 

LDR R4,R6,#0
ADD R6,R6,#1
LDR R5,R6,#0
ADD R6,R6,#1
LDR R3,R6,#0
ADD R6,R6,#1
LDR R2,R6,#0
ADD R6,R6,#1
LDR R1,R6,#0
ADD R6,R6,#1
LDR R7,R6,#0
ADD R6,R6,#1
RET


GETMAX ;; R0 = MAX(R0,R1)
ST R1,M1
ST R2,M2
ADD R2,R1,#0
NOT R2,R2
ADD R2,R2,#1
ADD R2,R2,R0
BRP CHOOSER0
ADD R0,R1,#0
CHOOSER0
LD R1,M1
LD R2,M2
RET
M1 .BLKW 1
M2 .BLKW 1
K3 .BLKW 1

GETMAPij ;r0=Matrix[i][j] r1 = i ,r2 = j ,A + I*M + J GET
ST  R2,M22
LD  R0,MAP
ADD R0,R0,R2 ; R0 = A+J
LDI R2,COL1 ; R2 = M
LOOPMAPIJ
BRZ DONEMAPIJ
ADD R0,R1,R0 ; R0+=I FOR m TIMES
ADD R2,R2,#-1
BR  LOOPMAPIJ
DONEMAPIJ
LD  R2,M22
RET
COL1 .FILL X4001
M22 .BLKW 1

.END

.ORIG x4000
.FILL #3 ; N
.FILL #4 ; M
.FILL #89 ; the map
.FILL #88
.FILL #86
.FILL #83
.FILL #79
.FILL #73
.FILL #90
.FILL #80
.FILL #60
.FILL #69
.FILL #73
.FILL #77
.END